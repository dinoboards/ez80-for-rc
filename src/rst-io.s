
	SECTION CODE

	.assume adl=1

INSTR_OUT_NN_A	EQU	%D3

	PUBLIC	_rst_io
_rst_io:
	PUSH	IX
	LD	IX, 3
	ADD	IX, SP

	PUSH	IY
	PUSH	HL
	PUSH	BC
	PUSH	AF

	;LD	A, (IX+0) 			; SHOULD BE 2H FOR Z80 MODE
	LD	A, (IX+1)
	LD	IYL, A
	LD	A, (IX+2)
	LD	IYH, A				; IY SHOULD BE RETURN ADDRESS, THE ADDRESS OF THE NEXT INSTR.
	LD.S 	A, (IY)  			; IS THIS THE FIST BYTE AFTER THE RST.L 8 INSTRUCTION.

	CP	INSTR_OUT_NN_A
	JR	Z, out_nn_a_hook

rst_io_resume:
	POP	AF
	POP	BC
	POP	HL
	POP	IY
	POP	IX

	NOP
	RET.L

out_nn_a_hook:
	LD.S	C, (IY+1)			; THE REQUEST PORT NUMBER
	LD	B, %FF

	POP	AF				; RETIEVE VALUE OF A BEFORE HOOK WAS INVOKED
	PUSH	AF
	; LD	A, (IX+5)			; OR GET IT VIA THE STACK VALUE

	OUT	(C), A

	INC	IY
	INC	IY

	LD	A, IYL
	LD	(IX+1), A			; UPDATE THE RETURN ADDRESS TO SKIP INTERPRETED
	LD	A, IYH				; INSTRUCTION
	LD	(IX+2), A

	jr	rst_io_resume
